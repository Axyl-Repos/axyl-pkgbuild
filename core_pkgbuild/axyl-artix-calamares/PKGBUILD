# Maintainer: Gabriel Matthews <axyl.os.linux@gmail.com>
pkgname=axyl-artix-calamares
_pkgname=calamares
pkgver=3.2.55
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
license=(GPL)
url="https://github.com/calamares/calamares"
license=('LGPL')
depends=('yaml-cpp' 'kpmcore>=3.1.0' 'boost-libs' 'hwinfo' 'gtk-update-icon-cache'
          'libpwquality' 'polkit-qt5' 'qt5-xmlpatterns' 'plasma-framework')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
optdepends=('calamares-branding: Artix branding')
source=("${url}/archive/v$pkgver.tar.gz"
        postcfg.patch
        services-runit.patch
        services-s6.patch
        services-suite66.patch
        services-dinit.patch
		calamares.desktop
		49-nopasswd-calamares.rules
		calamares_polkit
		)
sha256sums=('d717f4dab8f2ebef37cdb536cb11941a37081897d84bcaf2aa2e02073ae1ee42'
            '77c2d46c511ad6ea4f2c9663007f9d5a4cc2c385223498982d3feb4e56becc68'
            '820d515f5788b7b55e3ebaa2357db643bc1d554e931649980d8b39a6a26b51e0'
            'c1c1ba7d14754cf284b75812323cf1799d6f55c6ea2c12b3e6a110855306b3f6'
            'b74faf40a76cc9648967fbfb79796c9e0f283d4088d0a200baed14fe923c6b8e'
            'e8fe3bd2204a527409e495f23bf9c59098d97c5d21afd97006a81ff4fc414e24'
            'aeedc261e49612222ef040d2caa24043fee480d980b581ab4fe9edcde7e1aae1'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176'
            '4c8b48518b0047672e835e0a6c8a66342b316ab8835cf4c331030de4830dcea2')

prepare() {
    cd $_pkgname-$pkgver
    # patches here
    patch -Np 1 -i $srcdir/postcfg.patch
    patch -Np 1 -i $srcdir/services-runit.patch
    patch -Np 1 -i $srcdir/services-s6.patch
    patch -Np 1 -i $srcdir/services-suite66.patch
    patch -Np 1 -i $srcdir/services-dinit.patch
}

build() {
	cd $_pkgname-$pkgver

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=OFF \
              -DINSTALL_CONFIG:BOOL=ON \
              -DINSTALL_POLKIT:BOOL=ON \
              -DBoost_NO_BOOST_CMAKE=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg packagechooser services-systemd \
                              dummyprocess dummypython dummycpp dummypythonqt"
        make
}

package() {
	cd $_pkgname-$pkgver/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
	install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
}
